        .TITLE  MBOOT - MODIFIED BOOTOADERS
;
;=======================================================================================================
;  Загрузчик RK611/RK07 (DM)
;  На входе в R0 передается # устройства
;=======================================================================================================
;
DMCSR=177440	
DMCS1	=+0				
DMWC	=+2				
DMCSR2	=+10			
DMER	=+14

		.ASECT
        .=173252
DMSTART:        
        MOV	    #DMCSR,R1		
        MOV	    R0,DMCSR2(R1)	; вписываем unit# 
	    MOV	    #0003,(R1)		; команда подтверждения установки тома для устройства типа RK06
                
2$:	    TSTB    (R1)			; ждем ответа от контроллера
	    BPL	    2$			    ;
	    TST	    (R1)			; была ошибка?
	    BPL	    4$			    ; нет
                
	    BIT	    #40,DMER(R1)	; ошибка неправильного типа диска?
	    BEQ	    6$			    ; нет
	    
; Неправильный тип диска - значит у нас RK07
	    RESET				    ; сброс контроллера
	    MOV	    R0,DMCSR2(R1)	; вписываем unit# 
	    MOV	    #2003,(R1)		; команда подтверждения установки тома для устройства типа RK07
                
3$:	    TSTB    	(R1)		; ждем ответа от контроллера
	    BPL	    3$			
	    TST	    (R1)			; была ошибка?
	    BMI	    6$			    ; да
                
4$:	    MOV	    #-512.,DMWC(R1) ; счетчик слов - 1 сектор
	    MOV	    (R1),R3			; получаем используемый тип диска
	    BIC	    #377,R3			; очищаем поле команды
	    BIS	    #21,R3			; вписываем команду чтения
	    MOV	    R3,(R1)			; посылаем команду в контроллер
                
5$:	    TSTB    	(R1)		; ждем ответа от контроллера
	    BPL	    5$			
	    TST	    (R1)			; была ошибка?
	    BPL	    7$			    ; нет

; обработка ошибочных ситуаций        
6$:	    RESET                   ; сбрасваем контроллер
        HALT                    ; уходим в пульт
	    BR	     DMSTART          ; перезапуск загрузчика
; запуск загруженного бутсектора                
7$:	    CLR	     PC				

;=======================================================================================================
;  Загрузчик RH70/RP06 (DB)
;  На входе в R0 передается # устройства
;=======================================================================================================

RHCS  =176700                 ; CSR
RHWC  =+2                     ; WORD COUNT
RHBA  =+4                     ; BUS ADDR
RHCS2 =+10                    ; CSR2
RHAS  =+16                    ; ATTEN
RHOF  =+32                    ; OFFSET

DBSTART: MOV     #RHCS,R1    
         MOV     R0,RHCS2(R1)   ; вписываем unit# 
         MOV     #071,R2        ; 34 - команда чтения
         MOV     #021,(R1)      ; команда подготовки к начальной загузке
         MOV     #014000,RHOF(R1)  ; отключение ЕСС, формат диска 22 сектора
         MOV     RHAS(R1),RHAS(R1) ; очистка всех сигналов внимание
                 
BOOTRP:  MOV     #-512.,RHWC(R1) ; счетчик слов - 1 сектор
         MOV     (R1),R3        ; CSR
         BIC     #377,R3        ; очищаем поле команды
         BIS     R2,R3          ; вписваем команду чтения
         MOV     R3,(R1)        ; отправляем команду в контроллер
2$:      TSTB    (R1)           ; ждем ответа от контроллера
         BPL     2$             
                 
         TST     (R1)           ; была ошибка?
         BPL     3$             ; нет
; контроллер вернул ошибку                 
         RESET                  ; сброс контроллера
         HALT                   ; вываливаемся в ODT
         BR		  DMSTART		; перезапускаем процесс загрузки
                 
3$:      BIC     #377,(R1)      ; очищаем поле команд в CSR
         CLR     PC             ; запускаем считанный загрузчик

;=======================================================================================================
;  Загрузчик с дискет MY
;  На входе в R0 передается # устройства
;=======================================================================================================
MYCSR = 172140

MYSTART: MOV    #MYCSR,R1
         MOV    #37,(R1)   ; Команда LOAD
10$:     TSTB   (R1)       ; ждем DRQ
         BPL    10$
         MOV    R0,2(R1)   ; отправляем # устройства
20$:     BIT    #40,(R1)   ; ждем DONE
         BEQ    20$
         TST    (R1)       ; были ошибки?
         BPL    30$        ; нет
         HALT              ; при ошибке вываливаемся в ODT
         BR     MYSTART
30$:     CLR    PC         ; запускаем считанный загрузчик
         
        .END 
        
